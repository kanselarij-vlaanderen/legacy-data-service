PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX step: <http://example.com/step/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX adms: <http://www.w3.org/ns/adms#>

SELECT DISTINCT ?docUrl ?created ?title ?publicatieDossierUrl ?statusLabel where {
  graph <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?subcase a dossier:Procedurestap ;
        dct:type <http://example.com/step/6f7d1086-7c02-4a80-8c60-5690894f70fc> ;
        mu:uuid ?subcaseId ;
        ext:bevatReedsBezorgdeDocumentversie ?doc .
    ?container dossier:Collectie.bestaatUit ?doc ;
              dct:type <https://data.vlaanderen.be/id/concept/AardWetgeving/BesluitVanDeVlaamseRegering> .
    ?doc mu:uuid ?docId ;
      dct:created ?created ;
      dct:title ?title .
    FILTER (?created > xsd:dateTime("2022-01-01T00:00:00+01:00"))
    ?decFlow dossier:doorloopt ?subcase;
             mu:uuid ?decFlowId .
    ?dossier dossier:Dossier.isNeerslagVan ?decFlow .
    OPTIONAL {
      ?pubFlow dossier:behandelt ?dossier;
            mu:uuid ?pubFlowId;
            adms:status ?status .
    }
  }
  BIND(CONCAT("https://kaleidos.vlaanderen.be/document/",STR(?docId)) AS ?docUrl)
  BIND(IF(BOUND(?pubFlowId), CONCAT("https://kaleidos.vlaanderen.be/publicaties/",STR(?pubFlowId),"/dossier"), "") AS ?publicatieDossierUrl)
  OPTIONAL { ?status skos:prefLabel ?statusLabel }
} ORDER BY DESC(?created)
