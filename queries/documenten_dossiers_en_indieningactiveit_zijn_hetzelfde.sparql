PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <https://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tl: <http://mu.semte.ch/vocabularies/typed-literals/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT DISTINCT ?besluitvormingsaangelegenheid ?maxPlannedStart ?dossierCount ?indieningCount WHERE {
  {
    SELECT ?besluitvormingsaangelegenheid MAX(?plannedStart) as ?maxPlannedStart COUNT(DISTINCT ?dossierStuk) as ?dossierCount COUNT(DISTINCT ?indieningStuk) as ?indieningCount WHERE {
      GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
           ?dossier a dossier:Dossier;
                 dossier:Dossier.bestaatUit ?dossierStuk ;
                 dossier:Dossier.isNeerslagVan ?besluitvormingsaangelegenheid .
            ?besluitvormingsaangelegenheid dossier:doorloopt ?procedurestap .
        ?indieningsActiviteit ext:indieningVindtPlaatsTijdens ?procedurestap ;
                                prov:generated ?indieningStuk .
        ?agendaActiviteit besluitvorming:vindtPlaatsTijdens ?procedurestap .
        ?agendaActiviteit besluitvorming:genereertAgendapunt ?agendapunt .
        ?agenda dct:hasPart ?agendapunt .
        ?agenda besluitvorming:isAgendaVoor ?meeting .
        ?meeting besluit:geplandeStart ?plannedStart .
        ?dossierStuk dct:title ?dossierStukTitle .
        ?indieningStuk dct:title ?indieningStukTitle .
      }
    }
  }
  FILTER (?dossierCount != ?indieningCount)
  FILTER (?maxPlannedStart < xsd:dateTime("2019-10-02T02:00:00Z"))
}
