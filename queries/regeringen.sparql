PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX g: <https://data.vlaanderen.be/ns/generiek#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

SELECT DISTINCT ?mandataris ?regering ?regeringsLabel ?regeringVan ?regeringTot ?titel ?mandaatStart ?mandaatEinde ?rangorde ?familyName ?gebruikteVoornaam WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?mandataris a mandaat:Mandataris .
    ?regering prov:hadMember ?mandataris .
    ?regering skos:prefLabel ?regeringsLabel .
    ?regering prov:qualifiedGeneration ?creatie .
    ?creatie prov:atTime ?regeringVan .
    OPTIONAL {
      ?regering prov:qualifiedInvalidation ?opheffing .
      ?opheffing prov:atTime ?regeringTot
    } .
    OPTIONAL { ?mandataris mandaat:isBestuurlijkeAliasVan ?person } .
    OPTIONAL { ?person persoon:gebruikteVoornaam ?gebruikteVoornaam } .
    OPTIONAL { ?person foaf:familyName ?familyName } .
    OPTIONAL { ?mandataris mandaat:start ?mandaatStart } .
    OPTIONAL { ?mandataris mandaat:einde ?mandaatEinde } .
    OPTIONAL { ?mandataris mandaat:rangorde ?rangorde } .
    OPTIONAL { ?mandataris dcterms:title ?titel } .
  }
} ORDER BY ?regeringVan ?mandaatStart ?rangorde
