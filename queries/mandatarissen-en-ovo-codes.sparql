PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX g: <https://data.vlaanderen.be/ns/generiek#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX sign: <http://mu.semte.ch/vocabularies/ext/handtekenen/>

SELECT DISTINCT ?mandataris ?regering ?regeringsLabel ?regeringVan ?regeringTot ?titel ?nieuwsbriefTitel ?mandaatTitel ?mandaatStart ?mandaatEinde ?rangorde ?familyName ?gebruikteVoornaam ?ovoCode
FROM <http://mu.semte.ch/graphs/public>
FROM <http://mu.semte.ch/graphs/system/users>
WHERE {
    ?mandataris a mandaat:Mandataris .
    ?regering prov:hadMember ?mandataris .
    ?regering skos:prefLabel ?regeringsLabel .
    ?regering prov:qualifiedGeneration ?creatie .
    ?creatie prov:atTime ?regeringVan .
    OPTIONAL {
      ?regering prov:qualifiedInvalidation ?opheffing .
      ?opheffing prov:atTime ?regeringTot
    } .
    ?mandataris mandaat:isBestuurlijkeAliasVan ?person  .
    ?person persoon:gebruikteVoornaam ?gebruikteVoornaam .
    ?person foaf:familyName ?familyName .
    ?mandataris mandaat:start ?mandaatStart .
    OPTIONAL { ?mandataris mandaat:einde ?mandaatEinde } .
    ?mandataris mandaat:rangorde ?rangorde .
    OPTIONAL { ?mandataris dcterms:title ?titel } .
    OPTIONAL { ?mandataris ext:nieuwsbriefTitel ?nieuwsbriefTitel } .
    OPTIONAL { ?mandataris org:holds / org:role / skos:prefLabel ?mandaatTitel } .
     OPTIONAL {
          ?user sign:isOndertekenaarVoor ?person .
          ?membership org:organization ?organisatie ;
                                 org:member ?user .
          ?organisatie org:identifier ?ovoCode .
       }
} ORDER BY ?regeringVan ?mandaatStart ?rangorde