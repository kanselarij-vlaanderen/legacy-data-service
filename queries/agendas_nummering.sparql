PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tl: <http://mu.semte.ch/vocabularies/typed-literals/>

SELECT DISTINCT ?agendaTitel ?geplandeStart ?agendapuntPrioriteit ?agendapuntTitel ?agendapuntAltTitel ?agenda ?meeting ?agendapunt WHERE {
  {
    SELECT DISTINCT ?agendapunt WHERE {
      ?agendapunt a besluit:Agendapunt .
      ?agendapunt ext:wordtGetoondAlsMededeling "false"^^tl:boolean .
    }
  }
  ?agenda dct:hasPart ?agendapunt .
  ?agenda besluitvorming:isAgendaVoor ?meeting .
  ?meeting besluit:geplandeStart ?geplandeStart .
  OPTIONAL { ?agenda dct:title ?agendaTitel } .
  OPTIONAL { ?agendapunt ext:prioriteit  ?agendapuntPrioriteit } .
  OPTIONAL { ?agendapunt dct:title ?agendapuntTitel } .
  OPTIONAL { ?agendapunt dct:alternative ?agendapuntAltTitel } .
} ORDER BY ?geplandeStart ?meeting ?agenda ?agendapuntPrioriteit
