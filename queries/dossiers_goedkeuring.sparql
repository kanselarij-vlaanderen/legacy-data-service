PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tl: <http://mu.semte.ch/vocabularies/typed-literals/>

SELECT DISTINCT ?dossier ?title ?shortTitle ?procedurestap ?agendapunt ?agendaPuntTitel WHERE {
    {
      SELECT DISTINCT ?dossier ?title WHERE {
        GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
          ?dossier a dossier:Dossier .
          ?dossier dct:title ?title .
          FILTER ( regex(?title, "goedkeuring", "i") )
        }
      }
    } UNION {
      {
        SELECT DISTINCT ?dossier ?shortTitle WHERE {
          GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
            ?dossier a dossier:Dossier .
            ?dossier dct:alternative ?shortTitle .
            FILTER ( regex(?shortTitle, "goedkeuring", "i") )
          }
        }
      }
    }
    ?dossier dossier:Dossier.isNeerslagVan ?besluitvormingsaangelegenheid .
    ?besluitvormingsaangelegenheid dossier:doorloopt ?procedurestap .
    ?behandelingVanAgendaPunt ext:beslissingVindtPlaatsTijdens ?procedurestap .
    ?behandelingVanAgendaPunt dct:subject ?agendapunt .
    ?agendapunt dct:title ?agendaPuntTitel .
}
