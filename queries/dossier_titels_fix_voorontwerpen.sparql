PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX dossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX besluitvorming: <https://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tl: <http://mu.semte.ch/vocabularies/typed-literals/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>


SELECT ?url ?datumMR ?agendapunt ?dossier ?korteTitel ?langeTitel ?procedurestapTitel ?procedurestapNaam ?procedurestapType WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
    ?dossier dossier:Dossier.isNeerslagVan ?besluitvormingsaangelegenheid ;
             mu:uuid ?dossierId .
    OPTIONAL { ?dossier dct:alternative ?korteTitel . }
    OPTIONAL { ?dossier dct:title ?langeTitel  . }
    ?besluitvormingsaangelegenheid dossier:doorloopt ?procedurestap ;
                                   mu:uuid ?besluitvormingsaangelegenheidId ;
                                   dct:title ?besluitvormingsaangelegenheidTitel  .
    ?procedurestap mu:uuid ?procedurestapId ;
                   dct:title ?procedurestapTitel ;
                   ^besluitvorming:vindtPlaatsTijdens ?agendaActivity .
    OPTIONAL { ?procedurestap ext:procedurestapNaam ?procedurestapNaam . }
    OPTIONAL { ?procedurestap dct:type ?procedurestapType . }
    ?agendaActivity besluitvorming:genereertAgendapunt ?agendapunt .
    ?agenda dct:hasPart ?agendapunt ;
            besluitvorming:isAgendaVoor ?meeting .
    ?meeting besluit:geplandeStart ?datumMR .
    FILTER ( ?datumMR < xsd:dateTime("2019-10-02T02:00:00Z"))
    FILTER ( REGEX(STR(?langeTitel), "- Voorontwerp", "gi") )
    BIND(CONCAT("https://kaleidos-test.vlaanderen.be/dossiers/",STR(?besluitvormingsaangelegenheidId),"/deeldossiers/",STR(?procedurestapId)) AS ?url)
  }
} ORDER BY ?datumMR ?besluitvormingsaangelegenheid
